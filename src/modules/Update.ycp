/**
 * Module:		Update.ycp
 *
 * Authors:		Anas Nashif <nashif@suse.de>
 *			Arvin Schnell <arvin@suse.de>
 *
 * Purpose:		Update module
 *
 * $Id$
 */
{
    module "Update";

    import "Installation";
    import "Packages";
    import "ProductFeatures";
    import "Stage";
    import "SuSERelease";
    import "Mode";

    // number of packages to install
    global integer packages_to_install = 0;

    // number of packages to update
    global integer packages_to_update = 0;

    // number of packages to remove
    global integer packages_to_remove = 0;

    // number of packages unknown (problematic) by update
    global integer unknown_packages = 0;

    // number of errors (packages?) returned by solver
    global integer solve_errors = 0;

    // Flag is set true if the user decides to delete unmaintained packages
    global boolean deleteOldPackages = nil;

    // don't allow upgrade only update
    global boolean disallow_upgrade = false;

    global boolean did_init1 = false;

    global boolean did_init2 = false;


    global integer last_runlevel = -1;


    global boolean backup_modified = true;
    global boolean backup_sysconfig = true;
    global boolean remove_old_backups = false;
    global string backup_path = "/var/adm/backup";


    // Only an update, NOT an upgrade
    global boolean onlyUpdateInstalled = nil;

    global string selected_selection = "";

    global boolean products_incompatible = false;


    /*
     *  Information about old and new product.
     *
     *  They do contain:
     *    name (string), e.g. "SuSE Linux"
     *    version (string), e.g. "9.1"
     *    nameandversion (string), e.g. "SuSE Linux 9.1"
     *
     *  They may contain:
     *    major (integer), e.g. "9"
     *    minor (integer), e.g. "1"
     */

    // Version of the targetsystem
    global map <string, any> installedVersion = $[];

    // Version of the source medium
    global map <string, any> updateVersion = $[];


    // Flag, if the basesystem have to be installed
    global boolean updateBasePackages = false;

    // counter for installed packages
    global integer packagesInstalled = 0;


    // see bug #40358
    global boolean manual_interaction = false;

    // are the products (installed and to update) compatible?
    boolean _products_compatible = nil;

    /*-----------------------------------------------------------------------
     * FATE #301844 - Tuning Update Features
     *-----------------------------------------------------------------------*/

    boolean ListOfRegexpsMatchesProduct (list <string> regexp_items, string product) {
	if (regexp_items == nil || regexp_items == []) return false;
	if (product == nil) {
	    y2error ("Product is nil");
	    return false;
	}
	
	boolean ret = false;
	foreach (string one_regexp, regexp_items, {
	    if (regexpmatch (product, one_regexp)) {
		y2milestone (">%1< is matching >%2<", product, one_regexp);
		ret = true;
		break;
	    }
	});
	
	y2milestone ("Returning %1", ret);
	return ret;
    }

    /**
     * Returns whether old packages should be removed (defined in control file).
     * True means - delete old RPMs when updating. (Functionality for FATE #301844).
     */
    global boolean DeleteOldPackages () {
	// changes deleteOldPackages variable
	boolean default_dop = ProductFeatures::GetBooleanFeature ("software", "delete_old_packages");
	if (default_dop == nil) {
	    y2error ("software/delete_old_packages not defined");
	    return false;
	}

	string installed_system = SuSERelease::ReleaseInformation (Installation::destdir);
	y2milestone ("Processing '%1' from '%2'", installed_system, Installation::destdir);

	if (installed_system == nil || installed_system == "") {
	    y2error ("Cannot find out installed system name");
	    return default_dop;
	}
	
	any reverse_dop_a = ProductFeatures::GetFeature ("software", "delete_old_packages_reverse_list");
	// No reverse rules defined
	if (reverse_dop_a == "") {
	    return default_dop;
	}
	// not a list or empty list
	list <string> reverse_dop = (list <string>) reverse_dop_a;
	if (reverse_dop == nil || reverse_dop == []) return default_dop;

	if (ListOfRegexpsMatchesProduct (reverse_dop, installed_system)) {
	    return (! default_dop);
	}

	return default_dop;
    }
    
    /**
     * Returns whether upgrade process should only update installed packages or
     * also install new packages. True means - do not upgrade, only update packages.
     * (Functionality for FATE #301844).
     */
    global boolean OnlyUpdateInstalled () {
	// changes onlyUpdateInstalled variable
	boolean default_ous = ProductFeatures::GetBooleanFeature ("software", "only_update_selected");
	if (default_ous == nil) {
	    y2error ("software/only_update_selected not defined");
	    return false;
	}

	string installed_system = SuSERelease::ReleaseInformation (Installation::destdir);
	y2milestone ("Processing '%1' from '%2'", installed_system, Installation::destdir);

	if (installed_system == nil || installed_system == "") {
	    y2error ("Cannot find out installed system name");
	    return default_ous;
	}
	
	any reverse_ous_a = ProductFeatures::GetFeature ("software", "only_update_selected_reverse_list");
	// No reverse rules defined
	if (reverse_ous_a == "") {
	    return default_ous;
	}
	// not a list or empty list
	list <string> reverse_ous = (list <string>) reverse_ous_a;
	if (reverse_ous == nil || reverse_ous == []) return default_ous;

	if (ListOfRegexpsMatchesProduct (reverse_ous, installed_system)) {
	    return (! default_ous);
	}

	return default_ous;
    }
    
    /**
     * Returns whether the installed product is supported for upgrade.
     * (Functionality for FATE #301844).
     */
    global boolean IsProductSupportedForUpgrade () {
	string installed_system = SuSERelease::ReleaseInformation (Installation::destdir);
	y2milestone ("Processing '%1' from '%2'", installed_system, Installation::destdir);
	
	if (installed_system == nil || installed_system == "") {
	    y2error ("Cannot find out installed system name");
	    return false;
	}

	any supported_products_a = ProductFeatures::GetFeature ("software", "products_supported_for_upgrade");
	// No products defined
	if (supported_products_a == "") {
	    y2warning ("No products_supported_for_upgrade defined");
	    return true;
	}
	// not a list or empty list
	list <string> supported_products = (list <string>) supported_products_a;
	if (supported_products == nil || supported_products == []) return true;

	if (ListOfRegexpsMatchesProduct (supported_products, installed_system)) {
	    return true;
	}

	return false;
    }


    /*-----------------------------------------------------------------------
     * GLOBAL FUNCTIONS
     *-----------------------------------------------------------------------*/


global list<string> SelectedProducts () {
    list<map<string,any> > selected
	= Pkg::ResolvableProperties ("", `product, "");
    selected = filter (map<string,any> p, selected, {
	return p["status"]:nil == `selected;
    });
    return maplist (map<string,any> p, selected, {
	return p["summary"]:"";
    });
}

/**
 * Check if installed product and product to upgrade to are compatible
 * @return boolean true if update is possible
 */
global boolean ProductsCompatible () {
    if (_products_compatible == nil)
    {
	if (Stage::normal ())
	{
	    // check if name of one of the products on the installation
	    // media is same as one of the installed products
	    // assuming that multiple products on installation media
	    // are compatible and compatibility is transitive
	    list<map<string,any> > inst =
		Pkg::ResolvableProperties ("", `product, "");
	    inst = filter (map<string,any> p, inst, {
		return p["status"]:nil == `installed;
	    });
	    list<string> inst_names = maplist (map<string,any> p, inst, {
		return p["name"]:"";
	    });
	    list<string> to_install = maplist (integer src,
		Pkg::SourceGetCurrent (true),
	    {
		map<string,string> prod_info = Pkg::SourceProduct (src);
		return prod_info["name"]:"";
	    });
	    // filter out empty products
	    to_install = filter (string o_p, to_install, { return o_p != ""; });

	    y2milestone ("Installed products: %1", inst_names);
	    y2milestone ("Products on installation media: %1", to_install);

	    // at least one product name found
	    if (size (to_install) > 0) {
		string equal_product = find (string i, inst_names, {
		    string found = find (string u, to_install, {
			return u == i;
		    });
		    return found != nil;
		});
		_products_compatible = equal_product != nil;
	    // no product name found
	    // bugzilla #218720, valid without testing according to comment #10
	    } else {
		y2warning ("No products found, setting product-compatible to 'true'");
		_products_compatible = true;
	    }
	}
	else
	{
	    _products_compatible = true; // FIXME this is temporary
	}
	y2milestone ("Products found compatible: %1", _products_compatible);
    }

    return _products_compatible;
}

global void IgnoreProductCompatibility () {
    _products_compatible = true;

}

    /**
     *
     */
    global define void Reset ()
    {
	y2milestone ("Calling: UpdateReset()");

	deleteOldPackages = DeleteOldPackages();
	y2milestone ("deleteOldPackages %1", deleteOldPackages);
	onlyUpdateInstalled = OnlyUpdateInstalled();
	y2milestone ("onlyUpdateInstalled %1", onlyUpdateInstalled);
	disallow_upgrade = false;

	manual_interaction = false;
	products_incompatible = false;
	_products_compatible = nil;

	backup_modified = true;
	backup_sysconfig = true;
	remove_old_backups = false;
	backup_path = "/var/adm/backup";
    }


    /**
     *
     */
    global define void fill_version_map (map <string, any>& data)
    {
	data["nameandversion"] = data["name"]:"?" + " " + data["version"]:"?";

	list <string> tmp0 = [];
	if (regexpmatch (data["version"]:"", " -")) {
	    splitstring (data["version"]:"", " -");
	}

	list <string> tmp1 = [];
	if (regexpmatch (tmp0[0]:"", "\.")) {
	    splitstring (tmp0[0]:"", ".");
	}

	integer tmp2 = tointeger (tmp1[0]:"-1");
	if (tmp2 >= 0)
	    data["major"] = tmp2;

	integer tmp3 = tointeger (tmp1[1]:"-1");
	if (tmp3 >= 0)
	    data["minor"] = tmp3;
    }


    /**
     * Read product name and version for the old and new release.
     * Fill installedVersion and updateVersion.
     * @return success
     */
    global define boolean GetProductName ()
    {
	installedVersion = $[];
	updateVersion = $[];

	// get old product name

	// cannot use product information from package manager
	// for pre-zypp products
	// #153576
	string old_name = SuSERelease::ReleaseInformation
	    (Installation::destdir);
	y2milestone("SuSERelease::ReleaseInformation: %1", old_name);

	// Remove 'Beta...' from product release
	if (regexpmatch (old_name, "Beta")) {
	    old_name = regexpsub (old_name, "^(.*)[ \t]+Beta.*$", "\\1");
	// Remove 'Alpha...' from product release
	} else if (regexpmatch (old_name, "Alpha")) {
	    old_name = regexpsub (old_name, "^(.*)[ \t]+Alpha.*$", "\\1");
	}

	integer p = findlastof (old_name, " ");
	if (p == nil)
	{
	    y2error ("release info <%1> is screwed", old_name);
	    installedVersion = $[];
	}
	else
	{
	    installedVersion["show"] = old_name;
	    installedVersion["name"] = substring (old_name, 0, p);
	    installedVersion["version"] = substring (old_name, p + 1);
	    fill_version_map (installedVersion);
	}

	// "minor" and "major" version keys
	// bug #153576, "version" == "9" or "10.1" or ...
	string inst_ver = installedVersion["version"]:"";
	if (inst_ver != "" && inst_ver != nil) {
	    // SLE, SLD, OES...
	    if (regexpmatch (inst_ver, "^[0123456789]+$")) {
		installedVersion["major"] = tointeger (inst_ver);
	    // openSUSE
	    } else if (regexpmatch (inst_ver, "^[0123456789]+\.[0123456789]+$")) {
		installedVersion["major"] = tointeger (regexpsub (inst_ver, "^([0123456789]+)\.[0123456789]+$", "\\1"));
		installedVersion["minor"] = tointeger (regexpsub (inst_ver, "^[0123456789]+\.([0123456789]+)$", "\\1"));
	    } else {
		y2error("Cannot find out major/minor from >%1<", inst_ver);
	    }
	} else {
	    y2error("Cannot find out version: %1", installedVersion);
	}
	
	if (Mode::test()) {
	    y2error ("Skipping detection of new system");
	    return true;
	}

	// get new product name

	integer num = size (Packages::theSources);
	if (num <= 0)
	{
	    y2error ("No source");
	    updateVersion["name"] = "?";
	    updateVersion["version"] = "?";
	    fill_version_map (updateVersion);
	    return false;
	}

	map new_product = Pkg::SourceProductData (Packages::theSources[num-1]:0);
	y2milestone ("First source product %1", new_product);
	if (new_product == nil)
	{
	    updateVersion["name"] = "?";
	    updateVersion["version"] = "?";
	    y2error("Cannot find out source details: %1", updateVersion);
	    fill_version_map (updateVersion);
	    return false;
	}

	// bugzilla #225256, use "label" first, then a "productname"
	updateVersion["show"] = new_product["label"]:nil;
	if (updateVersion["show"]:nil == nil) {
	    y2warning ("No \"label\" defined in product");
	    updateVersion["show"] = new_product["productname"]:"?" + " " + new_product["productversion"]:"?";
	}
	updateVersion["name"] = new_product["label"]:new_product["productname"]:"?";
	updateVersion["version"] = new_product["productversion"]:"?";
	fill_version_map (updateVersion);

	string new_ver = updateVersion["version"]:"";
	if (new_ver != "" && new_ver != nil) {
	    // SLE, SLD, OES...
	    if (regexpmatch (new_ver, "^[0123456789]+$")) {
		updateVersion["major"] = tointeger (new_ver);
	    // openSUSE
	    } else if (regexpmatch (new_ver, "^[0123456789]+\.[0123456789]$")) {
		updateVersion["major"] = tointeger (regexpsub (new_ver, "^([0123456789]+)\.[0123456789]$", "\\1"));
		updateVersion["minor"] = tointeger (regexpsub (new_ver, "^[0123456789]+\.([0123456789])$", "\\1"));
	    } else {
		y2error("Cannot find out major/minor from %1", new_ver);
	    }
	} else {
	    y2error("Cannot find out version: %1", updateVersion);
	}

	y2milestone ("update from %1 to %2", installedVersion, updateVersion);

	return true;
    }

    global list<string> GetBasePatterns ()
    {
        // get available base patterns
        list<map<string,any> > patterns = Pkg::ResolvableProperties ("", `pattern, "");
        patterns = filter (map<string,any> p, patterns, {
            if (p["status"]:nil != `selected && p["status"]:nil != `available)
                return false;
            // if type != base
            return true;
        });
	return maplist (map<string,any> p, patterns, {
	   return p["name"]:"";
	});
    }


    /**
     * Get all available base selections sorted in reverse order
     * (highest ordered bases selection comes first).
     */
    global define list<string> GetBaseSelections ()
    {
	list<string> available_base_selections = sort (string x, string y, Pkg::GetSelections (`available, "base"), {
	    map xmap = Pkg::SelectionData(x);
	    map ymap = Pkg::SelectionData(y);
	    return (xmap["order"]:"" > ymap["order"]:"");
	});
	y2milestone ("available_base_selections %1", available_base_selections);
	return available_base_selections;
    }

    string ReadInstalledDesktop() {
	SCR::Execute (.target.bash, "/bin/mv -f /etc/sysconfig/windowmanager /etc/sysconfig/windowmanager.old");
	SCR::Execute (.target.bash, "/bin/ln -s /mnt/etc/sysconfig/windowmanager /etc/sysconfig/windowmanager");
	string ret = (string)SCR::Read (.sysconfig.windowmanager.DEFAULT_WM);
	SCR::Execute (.target.bash, "/bin/rm -f /etc/sysconfig/windowmanager");
	SCR::Execute (.target.bash, "/bin/mv -f /etc/sysconfig/windowmanager.old /etc/sysconfig/windowmanager");
	return ret;
    }

    global void SetDesktopPattern() {
	string desktop = ReadInstalledDesktop();
	if (desktop == "kde" || desktop == "gnome")
	{
	    y2milestone ("Selecting pattern to install: %1", desktop);
	    Pkg::ResolvableInstall (desktop, `pattern);
	}
    }

    /**
     * Propose a selection for the update and save it's name in
     * Update::selected_selection.
     */
    global define void ProposeSelection ()
    {
	selected_selection = "";

	list available_selections = GetBaseSelections ();

	// nulth try: teh default desktop

	string desktop = ReadInstalledDesktop();
	map wm2sel = $[
	    "kde" : "default",
	    "gnome" : "default-Gnome",
	];
	selected_selection = wm2sel[desktop]:"";

	if (!contains (available_selections, selected_selection))
	    selected_selection = "";

	if (selected_selection != "")
	{
	    y2milestone ("using default desktop %1 to define selection: %2",
		desktop, selected_selection);
	    return;
	}

	// first try: installed one

	list tmp1 = Pkg::GetSelections (`installed, "base");
	if (tmp1 != nil && tmp1 != [])
	    selected_selection = tmp1[0]:"";

	if (!contains (available_selections, selected_selection))
	    selected_selection = "";

	if (selected_selection != "") {
	    y2milestone ("using installed selection: %1", selected_selection);
	    return;
	}

	// second try: available selection with highest order

	if (size (available_selections) > 0)
	    selected_selection = available_selections[0]:"";

	if (selected_selection != "") {
	    y2milestone ("using highest order selection: %1", selected_selection);
	    return;
	}

	// error: no selection available

	y2error ("no selection available");

    }


    /**
     *
     */
    global define void Detach ()
    {
	Pkg::TargetFinish ();
	did_init1 = false;
	did_init2 = false;
    }

    void TextsUsedInFuture () {
	// TRANSLATORS: check-box, it might happen that we need to downgrade some packages during update
	string aaa = _("Allow Package Downgrade");
    }

}

/**
 * Module:		Update.ycp
 *
 * Authors:		Anas Nashif <nashif@suse.de>
 *			Arvin Schnell <arvin@suse.de>
 *
 * Purpose:		Update module
 *
 * $Id$
 */
{
    module "Update";

    import "Packages";

    // number of packages affected by update
    global integer affected_packages = 0;

    // number of packages unknown (problematic) by update
    global integer unknown_packages = 0;

    // number of errors (packages?) returned by solver
    global integer solve_errors = 0;

    // if the kernel was affected in _normal_ update mode
    global boolean kernel_affected = false;

    // Flag is set true if the user decides to delete unmaintained packages
    global boolean deleteOldPackages = true;

    // don't allow upgrade only update
    global boolean disallow_upgrade = false;

    global boolean did_init1 = false;

    global boolean did_init2 = false;


    global integer last_runlevel = -1;


    global boolean backup_modified = true;
    global boolean backup_sysconfig = true;
    global boolean remove_old_backups = false;
    global string backup_path = "/var/adm/backup";


    // Only an update, NOT an upgrade
    global boolean onlyUpdateSelected = false;

    global string selected_selection = "";


    // Name of the targetsystem
    global string installedName = "";

    // Version of the targetsystem
    global string installedVersion = "";

    // Version of the source medium
    global string updateVersion = "";

    // Flag, if the basesystem have to be installed
    global boolean updateBasePackages = false;

    // counter for installed packages
    global integer packagesInstalled = 0;



    /*-----------------------------------------------------------------------
     * GLOBAL FUNCTIONS
     *-----------------------------------------------------------------------*/


    /**
     *
     */
    global define boolean GetProductName () ``{

	// get old product name

	list old_products = Pkg::TargetProducts ();
	map old_product = old_products[0]:$[];
	y2milestone ("First installed product %1", old_product);

	installedVersion = old_product["productname"]:"?" + " " +
	    old_product["productversion"]:"";

	// get new product name

	integer num = size (Packages::theSources);
	if (num <= 0)
	{
	    y2error ("No source");
	    updateVersion = "?";
	    return false;
	}

	map new_product = Pkg::SourceProductData (Packages::theSources[num-1]:0);
	y2milestone ("First source product %1", new_product);
	if (product == nil)
	{
	    y2error ("Screwed source");
	    updateVersion = "?";
	    return false;
	}

	updateVersion = new_product["productname"]:"?" + " " +
	    new_product["productversion"]:"";

	y2milestone ("update from %1 to %2", installedVersion, updateVersion);

	return true;
    }


    /**
     * Get all available base selections sorted in reverse order
     * (highest ordered bases selection comes first).
     */
    global define list GetBaseSelections ()
    ``{
	list available_base_selections = sort (string x, string y, Pkg::GetSelections (`available, "base"), ``{
	    map xmap = Pkg::SelectionData(x);
	    map ymap = Pkg::SelectionData(y);
	    return (xmap["order"]:"" > ymap["order"]:"");
	});
	y2milestone ("available_base_selections %1", available_base_selections);
	return available_base_selections;
    }


    /**
     * Propose a selection for the update and save it's name in
     * Update::selected_selection.
     */
    global define void ProposeSelection () ``{

	selected_selection = "";

	list available_selections = GetBaseSelections ();

	// first try: installed one

	list tmp1 = Pkg::GetSelections (`installed, "base");
	if (tmp1 != nil && tmp1 != [])
	    selected_selection = tmp1[0]:"";

	if (!contains (available_selections, selected_selection))
	    selected_selection = "";

	if (selected_selection != "") {
	    y2milestone ("using installed selection: %1", selected_selection);
	    return;
	}

	// second try: available selection with highest order

	if (size (available_selections) > 0)
	    selected_selection = available_selections[0]:"";

	if (selected_selection != "") {
	    y2milestone ("using highest order selection: %1", selected_selection);
	    return;
	}

	// error: no selection available

	y2error ("no selection available");

    }


    /**
     *
     */
    global define void Detach () ``{

	Pkg::TargetFinish ();
	did_init1 = false;
	did_init2 = false;

    }

}

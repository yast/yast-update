/**
 * Module:	proposal_packages.ycp
 *
 * Author:	Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	Let user choose packages during update.
 */
{
    textdomain "update";

    import "HTML";
    import "SpaceCalculation";


    string func  = WFM::Args(0);
    map    param = WFM::Args(1);
    map    ret   = $[];


    define symbol call_packageselector () ``{

	if (Update::unknown_packages > 0) // FIXME
	{
	    UI::OpenDialog (`opt(`defaultsize), `PackageSelector (`id(`selector),
								  `opt(`updateMode)));
	}
	else
	{
	    UI::OpenDialog (`opt(`defaultsize), `PackageSelector (`id(`selector)));
	}

	Pkg::SaveState();

	symbol ret = UI::RunPkgSelection (`id(`selector));
	if (ret == `accept)
	{
	    Pkg::ClearSaveState();

	    // FIXME: IT'S A MESS
	    Update::solve_errors = 0;
	    Update::unknown_packages = 0;
	}
	else
	{
	    Pkg::RestoreState();
	}

	UI::CloseDialog();

	return ret;
    }


    if ( func == "MakeProposal" )
    {
	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	// call some function that makes a proposal here:
	//
	// DummyMod::MakeProposal( force_reset );

	// Fill return map

	SpaceCalculation::ShowPartitionWarning ();

	list tmp = [];

	// proposal for packages during update
	tmp = add (tmp, sformat (_("Affected Packages: %1"),
				 Update::affected_packages));

	if (Update::solve_errors > 0)
	{
	     // the proposal for the packages requires manual intervention
	    ret = $[ "preformatted_proposal" : HTML::List (tmp),
			/* warning text */
		     "warning" : _("Requires manual intervention"),
		     "warning_level" : `blocker ];
	}
	else
	{
	    ret = $[ "preformatted_proposal" : HTML::List (tmp) ];
	}
    }
    else if ( func == "AskUser" )
    {
	boolean has_next = param["has_next"]:false;

	// call some function that displays a user dialog
	// or a sequence of dialogs here:
	//
	// sequence = DummyMod::AskUser( has_next );

	symbol result = call_packageselector ();

	// FIXME: IT'S A MESS
	/*
	if (Pkg::PkgSolve (!Update::onlyUpdateSelected))
	    Update::solve_errors = 0;
	else
	    Update::solve_errors = Pkg::PkgSolveErrors ();
	*/

	// Fill return map

	ret = $[ "workflow_sequence" : result ];
    }
    else if ( func == "Description" )
    {
	// Fill return map.
	//
	// Static values do just nicely here, no need to call a function.

	ret =
	    $[
	      // this is a heading
	      "rich_text_title"	:	_("Packages"),
	      // this is a menu entry
	      "menu_title"	:	_("&Packages"),
	      "id"		:	"packages_stuff"
	    ];
    }

    return ret;
}

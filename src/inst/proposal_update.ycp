/**
 * Module:	proposal_update.ycp
 *
 * Author:	Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	Let user choose update settings.
 */
{
    textdomain "update";

    import "HTML";
    import "Update";
    import "Mode";
    import "Language";
    import "Packages";
    import "PackageCallbacks";


    string func  = WFM::Args(0);
    map    param = WFM::Args(1);
    map    ret   = $[];


    define void init_stuff () ``{

	// initialize package manager
	if (true)
	{
	    UI::OpenDialog (`opt(`decorated ),
			    // intermediate popup while initializing internal packagemanagement
			    `Label(_("Reading package information. One moment please...")));

	    Packages::Init (true);

	    UI::CloseDialog();

	    Update::GetProductName ();
	}

	// initialize target
	if (true)
	{
	    PackageCallbacks::SetConvertDBCallbacks ();

	    Pkg::TargetInit (Installation::destdir, false);
	}

	// connect target with package manager
	if (true)
	{
	    list tmp1 = Pkg::PkgUpdateAll (Update::deleteOldPackages);
	    Update::affected_packages = tmp1[0]:0;

	    if (Pkg::PkgSolve (!Update::onlyUpdateSelected))
		Update::solve_errors = 0;
	    else
		Update::solve_errors = Pkg::PkgSolveErrors ();
	}

    }


    if ( func == "MakeProposal" )
    {
	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	// call some function that makes a proposal here:
	//
	// DummyMod::MakeProposal( force_reset );

	// Fill return map

	init_stuff ();

	if (true)
	{
	    // get all base selections
	    // and sort them by reverse order
	    list available_base_selections = sort (`x, `y, Pkg::GetSelections (`available, "base"), ``{
		map xmap = Pkg::SelectionData(x);
		map ymap = Pkg::SelectionData(y);
		return (xmap["order"]:"" >= ymap["order"]:"");
	    });
	    y2milestone ("available_base_selections %1", available_base_selections);

	    if (size (available_base_selections) == 0)
	    {
		y2error ("No base selections available");
		// Can't find any software data, probably a installation media error
		// error message
		return $[ "warning" : _("Cannot read package data from installation media. Media error?"),
			  "warning_level" : `blocker ];
	    }
	}

	list tmp = [];

	tmp = add (tmp, sformat (_("Update to %1"), Update::updateVersion));

	if (Update::onlyUpdateSelected)
	{
	    // Proposal for backup during update
	    tmp = add (tmp, _("Only Update"));
	}
	else
	{
	    string selection = Update::GetSelection ();
	    if (selection == "")					// hmm, no matching selection
	    {
		list base_selections = Update::GetBaseSelections ();	// get highest ordered base as default
		selection = base_selections[0]:"";
	    }
	    map selection_data = Pkg::SelectionData (selection);
	    string selection_summary = selection_data["summary"]:"error";

	    // Proposal for backup during update
	    tmp = add (tmp, sformat (_("Update Based on Selection \"%1\""),
				     selection_summary));
	}

	if (Update::deleteOldPackages)
	{
	    // Proposal for backup during update
	    tmp = add (tmp, _("Delete Old Packages"));
	}

	ret = $[ "preformatted_proposal" :
		 HTML::List (tmp)
	];
    }
    else if ( func == "AskUser" )
    {
	boolean has_next = param["has_next"]:false;

	// call some function that displays a user dialog
	// or a sequence of dialogs here:
	//
	// sequence = DummyMod::AskUser( has_next );

	symbol result = WFM::CallFunction (`inst_update (true, has_next));

	// Fill return map

	ret = $[ "workflow_sequence" : result ];
    }
    else if ( func == "Description" )
    {
	// Fill return map.
	//
	// Static values do just nicely here, no need to call a function.

	ret =
	    $[
	      // this is a heading
	      "rich_text_title"	:	_("Update Options"),
	      // this is a menu entry
	      "menu_title"	:	_("&Update Options"),
	      "id"		:	"update_stuff"
	    ];
    }

    return ret;
}

/**
 * Module: 		inst_rpmupdate.ycp
 *
 * Authors:		Stefan Schubert (schubi@suse.de)
 *
 * Purpose:
 * Install, Update and remove all the RPM packages the user has selected.
 * Show installation dialogue. Show progress bars.
 * Request CD change from user.
 *
 * $Id$
 */

{
    textdomain "update";

    import "Arch";
    import "Report";
    import "Mode";
    import "Installation";
    import "Language";

    import "Update";

    import "PackageInstallation";
    import "SpaceCalculation";
    import "Packages";

    import "SlideShow";

    include "showlog_defines.ycp";
    include "ui/common_popups.ycp";


    string rebuild_message = _("Checking package database. This process can take some time.");
    string rebuild_message_error = _("Checking package database has returned an error.
Check the log file /var/log/y2log
");

    integer maxnumbercds = 8;

    //
    // Popup to start rpm --rebuild
    //

    // progress callback
    define void setProgress (integer progress)
    ``{
	UI::ChangeWidget(`id(`progress), `Value, 0 );
    }

    define void rpmrebuild( )
    ``{
	UI::OpenDialog(`opt(`decorated),
			`VBox( `Heading( _("RPM DB rebuild")) ,
			     `VBox( `Label(rebuild_message),
				    `HSpacing(60),
				    `HBox(
					  `HSpacing(2),
					  `ProgressBar(`id(`progress),
							 _("Status"), 100),
					  `HSpacing(2)
					  ),
				    `VSpacing(1)
				    )
			     )
			); // UI::OpenDialog()

	setProgress (0);
	Pkg::CallbackProgressRebuildDB ("setProgress");

	Pkg::TargetRebuildDB();

	UI::CloseDialog();

	return;
    };



    ////////////////////////////////////////////////////////////////////////////
    //  MAIN
    ////////////////////////////////////////////////////////////////////////////

    list base_packages = [];

    string tmpdir	= SCR::Read(.target.tmpdir);
    string language	= Language::language;

    SlideShow::SetLanguage( language );
    SlideShow::OpenSlideShowDialog();

    if ( Mode::cont )
    {
	y2milestone("Starting second part of package update");

	// Initalize package agent
	Update::Restore();
    }

    if ( Mode::initial )
    {
	// /etc/inittab will be overridden by package aaa_base. So we will
	// have to save the run-level

	string idfile = SCR::Read(.target.tmpdir) + "/idline";

	if ( SCR::Execute (.target.bash, "/bin/grep ^id: " + Installation::destdir + "/etc/inittab >" + idfile) == 0 )
	{
	    // idline = "id:X:initdefault:"

	    string idline = SCR::Read(.target.string, idfile);

	    // idsplit = "id", "X", "initdefault", ""
	    list idsplit = splitstring (idline, ":");

	    integer initdefault = -1;
	    if (size (idsplit) > 2)
	    {
		initdefault = tointeger (idsplit[1]:"3");
	    }

	    y2milestone("run-level %1 found", initdefault);

	    Update::last_runlevel = initdefault;
	}
    }

    SlideShow::InitPkgData();

    if ( !Mode::cont )
    {
	y2milestone ("rpmrebuild");
	// rebuilding RPM-Database only while first run of installation
	rpmrebuild();
    }

    any ret = `ok;

    boolean kernel_affected = false;

    if (Mode::normal)
    {
	list filterKernel = filter(string k, Pkg::GetPackages(`installed, true), ``(substring(k, 0, 2) == "k_" ));
	kernel_affected = (size (filterKernel) > 0);
    }

    list commit_result = [];

    SlideShow::UpdateAllCdProgress (false);
    SlideShow::StartTimer();

    if ( Mode::initial )
    {
	y2milestone ("Pkg::PkgCommit(1)");
	commit_result = Pkg::PkgCommit(1);
    }
    else
    {
	y2milestone ("Pkg::PkgCommit(0)");
	commit_result = Pkg::PkgCommit(0);
    }

    SlideShow::StopTimer();

    //*********************************************************

	integer count = commit_result[0]:0;
	y2milestone ("%1 packages installed", count);

	list failed = commit_result[1]:[];
	if (size (failed) > 0)
	{
	    y2milestone ("failed: %1", failed);
	    list previous_failed = SCR::Read (.target.ycp, Installation::destdir + "/var/lib/YaST2/failed_packages", []);
	    if (size (previous_failed) > 0)
		failed = union (previous_failed, failed);
	    SCR::Write (.target.ycp, Installation::destdir + "/var/lib/YaST2/failed_packages", failed);
	}
	list remaining = commit_result[2]:[];
	if (size (remaining) > 0)
	{
	    y2milestone ("remaining: %1", remaining);
	    SCR::Write (.target.ycp, Installation::destdir + "/var/lib/YaST2/remaining", remaining);
	}


    //*********************************************************

    if (kernel_affected)			// kernel affected
    {
	y2milestone ("Boot::Write()");
	import "Boot";
	Boot::Write();				// re-write initrd and bootloader
    }

    // Now that we know how many packages to install, we can show the dialog

    y2milestone("%1 packages have been installed", Update::packagesInstalled);

    if (Mode::cont)
    {
	SCR::Execute(.target.remove, "/var/lib/YaST2/runme_at_boot");
    }

    // close "Slide" dialog (inst_rpmupdate doesn't use the "Wizard" dialog)
    UI::CloseDialog();

    return `next;
}
